macro(try_static_libs_if_needed)
    if (TRY_STATIC_LIBS)
        list(GET CMAKE_FIND_LIBRARY_PREFIXES 0 STATIC_LIB_PREFIX)
        list(APPEND DEPS_STATIC_LIBRARY_DIRS "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
        if(APPLE)
            set(DYNAMIC_LIB_EXTS ".dylib" ".tbd")
            set(STATIC_LIB_EXT ".a")
            list(APPEND DEPS_LIBRARY_DIRS "/usr/lib" "${CMAKE_OSX_SYSROOT}/usr/lib" "/usr/local/lib" "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
        elseif(UNIX)
            set(DYNAMIC_LIB_EXTS ".so")
            set(STATIC_LIB_EXT ".a")
        elseif(WIN32 OR MINGW)
            set(DYNAMIC_LIB_EXTS ".dll")
            set(STATIC_LIB_EXT ".lib")
        endif()
        set(TRY_STATIC_DEPS_LDFLAGS "")
        foreach(LIB_NAME ${DEPS_STATIC_LIBRARIES})
            foreach(LIB_DIR ${DEPS_STATIC_LIBRARY_DIRS})
                set(LIB_FULLPATH "${LIB_DIR}/${STATIC_LIB_PREFIX}${LIB_NAME}${STATIC_LIB_EXT}")
                if(EXISTS ${LIB_FULLPATH})
                    message("[Info] Found static lib '${LIB_NAME}' for '${PROJECT_NAME}': ${LIB_FULLPATH}")
                    list(APPEND TRY_STATIC_DEPS_LDFLAGS ${LIB_FULLPATH})
                    set(${LIB_NAME}_FOUND YES)
                    break()
                endif()
            endforeach()
            if(NOT ${LIB_NAME}_FOUND)
                foreach(LIB_DIR ${DEPS_LIBRARY_DIRS})
                    foreach(DYNAMIC_LIB_EXT ${DYNAMIC_LIB_EXTS})
                        set(LIB_FULLPATH "${LIB_DIR}/${STATIC_LIB_PREFIX}${LIB_NAME}${DYNAMIC_LIB_EXT}")
                        if(EXISTS ${LIB_FULLPATH})
                            list(PREPEND TRY_STATIC_DEPS_LDFLAGS "-l${LIB_NAME}")
                            set(${LIB_NAME}_FOUND YES)
                            message("[Note] Could not find static lib '${LIB_NAME}' for '${PROJECT_NAME}', using found shared lib instead: ${LIB_FULLPATH}")
                            break()
                        endif()
                    endforeach()
                    if(${LIB_NAME}_FOUND)
                        break()
                    endif()
                endforeach()
                if(NOT ${LIB_NAME}_FOUND)
                    message("[Warning] Could not find static nor shared lib '${LIB_NAME}' for '${PROJECT_NAME}'. Adding '-l${LIB_NAME}' directly anyway!")
                    list(APPEND TRY_STATIC_DEPS_LDFLAGS "-l${LIB_NAME}")
                endif()
            endif()
        endforeach()
        foreach(LIB_DIR ${DEPS_LIBRARY_DIRS})
            list(PREPEND TRY_STATIC_DEPS_LDFLAGS "-L${LIB_DIR}")
        endforeach()
        string(REPLACE ";" " " DEPS_STATIC_LDFLAGS_OTHER "${DEPS_STATIC_LDFLAGS_OTHER}")
        list(APPEND TRY_STATIC_DEPS_LDFLAGS "${DEPS_STATIC_LDFLAGS_OTHER}")
    endif()
endmacro()
